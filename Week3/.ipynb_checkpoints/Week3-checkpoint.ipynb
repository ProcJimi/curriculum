{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "*Ex1_Numeric_Character_Formats.sas (Part 1);\n",
    "\n",
    "* This code example below shows only single (not multiple) CHARACTER values on the left side of the = sign \n",
    "  in the PROC FORMAT VALUE statement."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "Title 'Format for character values';\n",
    "options nocenter nodate nosource;\n",
    "proc format;\n",
    "value $regionfmt\n",
    "    'AFR' = 'Africa'\n",
    "    'AMR' = 'Americas'\n",
    "    'EUR' = 'Europe'\n",
    "    'EMR'  ='Eastern Mediterranean'\n",
    "    'SEAR' = 'South-East Asia'\n",
    "    'WPR' = 'Western Pacific';\n",
    "      run;\n",
    "proc freq data=sashelp.demographics; \n",
    "  tables region; \n",
    "  format region $regionfmt.;\n",
    "run;\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Ex1_Numeric_Character_Formats.sas (Part 2);\n",
    "\n",
    "In the example below, note the ranges on the left side of the = sign in PROC FORMAT VALUE statement with keywordS: LOW, and OTHER."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "Title 'Format for numeric values';\n",
    "options nocenter nodate nosource;\n",
    "proc format;\n",
    "  value numfmt\n",
    "           Low - <0  = \"Nonresponse\"\n",
    "           0=\"Never\"\n",
    "           1-5 = \"Within past 5 years\"\n",
    "           6-High = \"More than 5 years ago\"\n",
    "           . =\"Missing\" ;\n",
    "   value $charfmt\n",
    "           Low-<'0'  = \"Nonresponse\"\n",
    "          '0' = \"Never\"\n",
    "           '1'-'5' = \"Within past 5 years\"\n",
    "           '6'-High = \"More than 5 years ago\" ; \n",
    " run;\n",
    "data work.have;\n",
    "input id $ 1 Colonoscopy 3-4 c_Colonoscopy $6-7;\n",
    "datalines;\n",
    "A -1 -1 \n",
    "B  . \n",
    "C 3   3\n",
    "D -9 -9\n",
    "F 3  3\n",
    "G 5  5\n",
    "H 6  6 \n",
    "I .   \n",
    "J 7  7\n",
    ";\n",
    "proc freq data=work.have;\n",
    "tables colonoscopy c_colonoscopy /nopercent nocum missing;\n",
    "Format colonoscopy numfmt. c_colonoscopy $charfmt.;\n",
    "run;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Ex1_Numeric_Character_Formats.sas (Part 3);\n",
    "\n",
    "In the code snippet below, the format values are ranges; the special keyword LOW is used \n",
    "to define the lowest value. \n",
    "\n",
    "Because it is a numeric format, LOW does not format missing values. Note that, for character \n",
    "formats, LOW includes missing or blank values.\n",
    "\n",
    "Important note: Here we are grouping data values using formats in the PROC FREQ step.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "Title 'Grouping data values using formats in the PROC FREQ step';\n",
    "options nocenter nodate nonumber nosource;\n",
    "proc format;\n",
    "     value wtfmt   low - <100 = 'Under 100 lbs'\n",
    "                 100 - high = '100+ lbs'; \n",
    "run;\n",
    "proc freq data=sashelp.class;\n",
    "   tables weight;\n",
    "   format weight wtfmt.;\n",
    "run;\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "*Ex2_Nested_Formats.sas;\n",
    "options nocenter nodate nosource;\n",
    "proc format;\n",
    "value date_grp_fmt\n",
    "    low-'03jul1995'd = 'Pre July 4th 1995'\n",
    "    '04jul1995'd-'31jul1995'd = [mmddyy8.]\n",
    "    '01aug1995'd-high = 'Aug 1-Dec 31, 1995';\n",
    "value sales_fmt\n",
    "    low-<5000 = 'Less than $5,000'\n",
    "    5000-9999 = '$5,000-<$10,000'\n",
    "    10000-high = [dollar12.2];\n",
    "  run;\n",
    "  title 'Nested formats';\n",
    "  proc freq data=sashelp.mdv;\n",
    "    tables shipdate sales93;\n",
    "    format shipdate date_grp_fmt.  sales93 sales_fmt.;\n",
    "  run;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Ex3_Picture_Statement.sas (Part 1)\n",
    "\n",
    "Use the PICTURE statement with PROC FORMAT to create templates \n",
    "for labeling numbers.\n",
    "\n",
    "There are three types of characters in pictures.\n",
    "* Digit selectors (e.g., 0 through 9)\n",
    "* Message characters (e.g., M for Million, B for Billion) \n",
    "* Directives\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "Proc format;\n",
    " picture week_fmt 1-52='99';\n",
    " picture week_x_fmt 1-52 ='00';\n",
    " run;\n",
    " data have;\n",
    " input week @@;\n",
    " datalines;\n",
    " 1 3 6 8 9 14 15 34 52\n",
    " ;\n",
    " proc print; run;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "options nocenter nodate nosource;\n",
    "title \"Non-zero digit selectors add zeros to the formatted value\";\n",
    "proc print data=have;\n",
    " var week;\n",
    " format week week_fmt.;\n",
    "run;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "options nocenter nodate nosource;\n",
    "title \"Zero digit selectors do not add zeros to the formatted value\";\n",
    "proc print data=have;\n",
    " var week;\n",
    " format week week_x_fmt.;\n",
    "run;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SAS",
   "language": "sas",
   "name": "sas"
  },
  "language_info": {
   "codemirror_mode": "sas",
   "file_extension": ".sas",
   "mimetype": "text/x-sas",
   "name": "sas"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
